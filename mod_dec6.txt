n m o z

# 6 columns decimal integer

:init
    set '0 z
:clean
    clear z
 
 :@check v
    is_var v:f v:e v:d v:c v:b v:a

:@set f e d c b a v
    @check v
    set f v:f
    set e v:e
    set d v:d
    set c v:c
    set b v:b
    set a v:a

:@w_move_str s v
    @check v
    # is str s

    @clear v

    s:@drop n
    while n
        @shl v
        copysub z n
        @w_movelowest n +v:a
        s:@drop +n
    endwhile n

:@shl v
    @check v
    move v:e v:f
    move v:d v:e
    move v:c v:d
    move v:b v:c
    move v:a v:b
    clear v:a
:@shr v
    @check v
    move v:b v:a
    move v:c v:b
    move v:d v:c
    move v:e v:d
    move v:f v:e
    clear v:f
:@r_copylowest v out
    @check v
    is_var out
    copy v:d out
:@r_movelowest v out
    @check v
    is_var out
    move v:d out
:@w_copylowest in v
    @check v
    is_var in
    copy in v:d
:@w_movelowest in v
    @check v
    is_var in
    move in v:d

:@clear v
    @set 0 0 0 0 0 0 v
:_@move _f x y
    @check x
    @check y
    _f x:f y:f
    _f x:e y:e
    _f x:d y:d
    _f x:c y:c
    _f x:b y:b
    _f x:a y:a
:@move x y
    _@move move x y
:@copy x y
    _@move _copy x y

 # via o
:_copyadd x y
    move x o
    moveadd o x y
:_copyadd6 x y0 y1 y2 y3 y4 y5
    move x o
    moveadd o x y0 y1 y2 y3 y4 y5
:_copy x y
    clear y
    _copyadd x y
:_copysub x y
    move x o
    movesub o x y
:_copysub6 x y0 y1 y2 y3 y4 y5
    move x o
    move o +x -y0 -y1 -y2 -y3 -y4 -y5
:_copyneg x y
    clear y
    _copysub x y


:_nop4 x _m _n _f
:_inc x _m _n  _f v  _next
    _copyadd v n
    sub _m n
    ifelse n o
        _f v
    else n -o
        set _n v
        _next x _m _n _f
    endifelse n o

:_inc_a x _m _n _f
    _inc x _m _n  _f x:a  _inc_b
:_inc_b x _m _n _f
    _inc x _m _n  _f x:b  _inc_c
:_inc_c x _m _n _f
    _inc x _m _n  _f x:c  _inc_d
:_inc_d x _m _n _f
    _inc x _m _n  _f x:d  _inc_e
:_inc_e x _m _n _f
    _inc x _m _n  _f x:e  _inc_f
:_inc_f x _m _n _f
    _inc x _m _n  _f x:f  _nop4

:@inc x
    @check x
    _inc_a x 9 0 inc
:@dec x
    @check x
    _inc_a x 0 9 dec

:_copyifgt9 v n o
    _copy v n
    set 9 m
    tmp +o
    ifgt n m
    tmp -o

:_endifgt9 n o
    tmp +o
    endifgt n m
    tmp -o

:_moveadd_dec _f_sub _f_inc _f_copyadd x y
    @check x
    @check y
    _f_copyadd x:a y:a
    _copyifgt9 y:a n o
        _f_sub 10 y:a
        _f_inc y:b
    _endifgt9 n o

    _f_copyadd x:b y:b
    _copyifgt9 y:b n o
        _f_sub 10 y:b
        _f_inc y:c
    _endifgt9 n o

    _f_copyadd x:c y:c
    _copyifgt9 y:c n o
        _f_sub 10 y:c
        _f_inc y:d
    _endifgt9 n o

    _f_copyadd x:d y:d
    _copyifgt9 y:d n o
        _f_sub 10 y:d
        _f_inc y:e
    _endifgt9 n o

    _f_copyadd x:e y:e
    _copyifgt9 y:e n o
        _f_sub 10 y:e
        _f_inc y:f
    _endifgt9 n o

    _f_copyadd x:f y:f
    _copyifgt9 y:f n o
        _f_sub 10 y:f
    _endifgt9 n o


:@moveadd x y
    _moveadd_dec sub inc moveadd  x y
:@copyadd x y
    _moveadd_dec sub inc _copyadd  x y
:@movesub x y
    _movesub_dec add dec movesub  x y
:@copysub x y
    _movesub_dec add dec _copysub  x y

# breaks x
:@movemul x y z
    @check x
    @check y
    @check z

    @while x
        @copyadd y z
        @dec x
    @endwhile x

:_copyifelse x n o
    _copyadd x n
    ifelse n o

:_copyif x n
    _copyadd x n
    if n

:@read x
    @check x
    input x:f x:e x:d x:c x:b x:a
    _copysub6 z x:f x:e x:d x:c x:b x:a

:@write0 x
    @check x
    _copyadd6 z x:f x:e x:d x:c x:b x:a
    print x:f x:e x:d x:c x:b x:a
    _copysub6 z x:f x:e x:d x:c x:b x:a

:@read_until tail x
    @check x
    @clear x
    input n
    sub tail n
    while n
        add tail n
        sub '0 n
        move x:e x:f
        move x:d x:e
        move x:c x:d
        move x:b x:c
        move x:a x:b
        move n x:a

        input n
        sub tail n
    endwhile n

:@readln x
    @read_until 10 x

:@writeln0 x
    @write0 x
    set 10 n
    print n
    clear n

:_write_n
    _copyadd z n
    print n
:_write1 x
    _copy x n
    _write_n

:@write x
    @check x
    _copy x:f n
    ifelse n m
        _write_n
        _write1 x:e
        _write1 x:d
        _write1 x:c
        _write1 x:b
    else n m
        _@write_1 x
    endifelse n m

    _copy x:a n
    _write_n

    clear n

:_@write_1 x
    _copy x:e n
    ifelse n m
        _write_n
        _write1 x:d
        _write1 x:c
        _write1 x:b
    else n m
        _@write_2 x
    endifelse n m

:_@write_2 x
    _copy x:d n
    ifelse n m
        _write_n
        _write1 x:c
        _write1 x:b
    else n m
        _@write_3 x
    endifelse n m

:_@write_3 x
    _copy x:c n
    ifelse n m
        _write_n
        _write1 x:b
    else n m
        _copy x:b n
        if n
            _write_n
        endif n
    endifelse n m

:@writeln x
    @write x
    set 10 n
    print n
    clear n

# moveadd to :f
:_movefold x
    @check x
    moveadd x:e x:f
    moveadd x:d x:f
    moveadd x:c x:f
    moveadd x:b x:f
    moveadd x:a x:f

:_copyfold x n
    @check x
    _copyadd x:f n
    _copyadd x:e n
    _copyadd x:d n
    _copyadd x:c n
    _copyadd x:b n
    _copyadd x:a n

:@if x
    @check x
    _copyfold x n
    if n
:@endif x
    @check x
    _copyfold x n
    endif n
    @clear x

:@ifelse x y
    @check x
    _copyfold x n
    ifelse n y
:@else x y
    @check x
    else n y
:@endifelse x y
    @check x
    endifelse n y
    @clear x

:@while x
    @check x
    _copyfold x n
    while n
    clear n
:@endwhile x
    @check x
    clear n
    _copyfold x n
    endwhile n


end

