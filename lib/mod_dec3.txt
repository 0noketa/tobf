n m e z

# 3 columns decimal integer

:init
    set '0 z
:clean
    clear z

:@check v
    is_var v:c v:b v:a

:@set c b a v
    @check v
    set c v:c
    set b v:b
    set a v:a

:@w_move_str s v
    @check v
    # is str s

    @clear v

    s:@drop n
    while n
        @shl v
        copysub z n
        @w_movelowest n +v:f
        s:@drop +n
    endwhile n

:@shl v
    @check v
    move v:b v:c
    move v:a v:b
    clear v:a
:@shr v
    @check v
    move v:b v:a
    move v:c v:b
    clear v:c
:@rol v
    move v:c n
    shl v
    move n v:a
:@ror v
    move v:a n
    shr v
    move n v:a
:@r_copylowest v out
    @check v
    is_var out
    copy v:a out
:@r_movelowest v out
    @check v
    is_var out
    move v:a out
:@w_copylowest in v
    @check v
    is_var in
    copy in v:a
:@w_movelowest in v
    @check v
    is_var in
    move in v:a
:@r_copyhighest v out
    @check v
    is_var out
    copy v:c out
:@r_movehighest v out
    @check v
    is_var out
    move v:c out
:@w_copyhighest in v
    @check v
    is_var in
    copy in v:c
:@w_movehighest in v
    @check v
    is_var in
    move in v:c

:@clear v
    @set 0 0 0 v
:@move x y
    @check x
    @check y
    move x:c y:c
    move x:b y:b
    move x:a y:a
:@copy x y
    @check x
    @check y
    _copy x:c y:c
    _copy x:b y:b
    _copy x:a y:a

 # via e
:_copyadd x y
    move x e
    moveadd e x y
:_copyadd3 x y0 y1 y2
    move x e
    moveadd e x y0 y1 y2
:_copy x y
    clear y
    _copyadd x y
:_copysub x y
    move x e
    movesub e x y
:_copysub3 x y0 y1 y2
    move x e
    move e +x -y0 -y1 -y2
:_copyneg x y
    clear y
    _copysub x y


:_nop2 x _m _n _f
:_inc x _m _n  _f v  _next
    _copyadd v n
    sub _m n
    ifelse n e
        _f v
    else n -e
        set _n v
        _next x _m _n _f
    endifelse n e

:_inc_a x _m _n _f
    _inc x _m _n  _f x:a  _inc_b
:_inc_b x _m _n _f
    _inc x _m _n  _f x:b  _inc_c
:_inc_c x _m _n _f
    _inc x _m _n  _f x:c  _nop2

:@inc x
    @check x
    _inc_a x 9 0 inc
:@dec x
    @check x
    _inc_a x 0 9 dec

:_copyifgt9 v n e
    _copy v n
    set 9 m
    ifgt n m e

:_endifgt9 n e
    endifgt n m e

:_moveadd_dec _f_sub _f_inc _f_copyadd x y
    @check x
    @check y
    _f_copyadd x:a y:a
    _copyifgt9 y:a n e
        _f_sub 10 y:a
        _f_inc y:b
    _endifgt9 n e

    _f_copyadd x:b y:b
    _copyifgt9 y:b n e
        _f_sub 10 y:b
        _f_inc y:c
    _endifgt9 n e

    _f_copyadd x:c y:c
    _copyifgt9 y:c n e
        _f_sub 10 y:c
    _endifgt9 n e


:@moveadd x y
    _moveadd_dec sub inc moveadd  x y
:@copyadd x y
    _moveadd_dec sub inc _copyadd  x y
:@movesub x y
    _movesub_dec add dec movesub  x y
:@copysub x y
    _movesub_dec add dec _copysub  x y

# breaks x
:@movemul x y z
    @check x
    @check y
    @check z

    @while x
        @copyadd y z
        @dec x
    @endwhile x

:_copyifelse x n e
    _copyadd x n
    ifelse n e

:_copyif x n
    _copyadd x n
    if n

:@read x
    @check x
    input x:c x:b x:a
    _copysub3 z x:c x:b x:a

:@write0 x
    @check x
    _copyadd3 z x:c x:b x:a
    print x:c x:b x:a
    _copysub3 z x:c x:b x:a

:@read_until tail x
    @check x
    @clear x
    input n
    sub tail n
    while n
        add tail n
        sub '0 n
        move x:b x:c
        move x:a x:b
        move n x:a

        input n
        sub tail n
    endwhile n

:@readln x
    @read_until 10 x

:@writeln0 x
    @write0 x
    set 10 n
    print n
    clear n

:_write_n
    _copyadd z n
    print n

:@write x
    @check x
    _copy x:c n
    ifelse n e
        _write_n
        _copy x:b n
        _write_n
    else n e
        _copy x:b n
        if n
            _write_n
        endif n
    endifelse n e

    _copy x:a n
    _write_n

    clear n
:@writeln x
    @write x
    set 10 n
    print n
    clear n

# moveadd to :c
:_movefold x
    @check x
    moveadd x:b x:c
    moveadd x:a x:c

:_copyfold x n
    @check x
    _copyadd x:c n
    _copyadd x:b n
    _copyadd x:a n

:@if x
    @check x
    _copyfold x n
    if n
:@endif x
    @check x
    _copyfold x n
    endif n
    @clear x

:@ifelse x y
    @check x
    _copyfold x n
    ifelse n y
:@else x y
    @check x
    else n y
:@endifelse x y
    @check x
    endifelse n y
    @clear x

:@while x
    @check x
    _copyfold x n
    while n
    clear n
:@endwhile x
    @check x
    clear n
    _copyfold x n
    endwhile n


end

