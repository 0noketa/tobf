next_label n current_label m
# changed from mod_jump:
#   @begin takes number of labels instead of first label
#   @end has no argument
#   when jumping happens  @goto and @goto_if disables following code until @at or @end

:init
    loadas local:cs consts
    local:cs:def 0  LABELS JUMPS STUB X

# N: number of labels
:@begin N
    is_val N

    local:cs:redef N LABELS
    local:cs:add 1 LABELS

    local:cs:redef 0 JUMPS STUB

    dup_bf_at next_label  local:cs:LABELS  +
    bf_at next_label  [[>+>+<<-]->[<+>-]>>+<
    dup_bf_at next_label  local:cs:LABELS  -[
    bf_at next_label  [[-]>-<]
    bf_at next_label  ]>[ <<<

# N: starts with 0  currently 255 and 254 cause error
:@at N
    is_val N

    bf_at next_label  >>> [-]
    dup_bf_at next_label  local:cs:JUMPS  ]
    bf_at next_label  ]<]>[

    local:cs:redef N X
    local:cs:sub local:cs:STUB X

    dup_bf_at next_label  local:cs:X  [-]]<]>[
    bf_at next_label  <<<

    local:cs:redef N STUB
    local:cs:inc STUB

    local:cs:redef 0 JUMPS

:@end
    bf_at next_label  >>> [-]
    dup_bf_at next_label  local:cs:JUMPS  ]
    bf_at next_label  ]<

    local:cs:redef local:cs:LABELS X
    local:cs:sub local:cs:STUB X
    local:cs:dec X

    dup_bf local:cs:LABELS L
    dup_bf local:cs:STUB S
    dup_bf local:cs:X X

    dup_bf_at next_label  local:cs:X  ]>[[-]]<

    bf_at next_label  <<]


# _set: any of set copy move or user_defined macro
# lbl: will be passed to _set as first argument  type depends on _set
:@goto _set lbl
    set local:cs:LABELS next_label
    _set lbl n
    movesub n next_label
    dec next_label

    bf_at next_label  >>[-]>[-][<<<

    local:cs:inc JUMPS

:@goto_if v _set lbl
    is_var v

    if v
        set local:cs:LABELS next_label
        _set lbl n
        movesub n next_label
        dec next_label

        clear current_label m
    endif v

    bf_at next_label  >>>[<<<

    local:cs:inc JUMPS

:@goto_ifn v _set lbl
    is_var v

    not v
    @goto_if v _set lbl

:@jmp N
    @goto set N

:@jz v N
    @goto_ifn v set N

end
