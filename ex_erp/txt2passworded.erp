( erp2bf ex_erp\txt2passworded.erp -rs10 -ds10 )
( text to passworded brainfuck
behavior of output:
    reads 16 bytes password input. password is first 16 bytes of source text.
    writes text that varies with input.  
)
import erp_base
import erp_while

ary pass 16
ary buf 16

var max_pass
var eof
var len
var i
var c
var c2
var p
var p2
var q

: load ( dst_addr -- )
    =p
    p max_pass + =q
    0 =len
    {
        p q <
        { getc =c  c 255 <> } and
    } {
        c p !
        p inc =p
        len inc =len
    } while

    len max_pass <>
    {
        1 =eof
    } 'noop if jmp

: shift
    'buf =p
    'pass =p2
    p max_pass + =q
    { p q < } {
        p @ p2 !

        p inc =p
        p2 inc =p2
    } while
    ;

: dump
    'buf =p
    'pass =p2
    0 =i
    { i len < } {
        p @ p2 @ put_as_bf
        p inc =p
        p2 inc =p2
        i inc =i
    } while

    len '<' put_repeated
    ln
    ;

: put_repeated ( num chr -- )
    =c
: .L1
    dup '.L2 '.L3 if jmp
: .L2
    c putc

    dec
    '.L1 jmp
: .L3
    drop
    ;


: put_as_bf ( chr password_chr -- )
    =c2
    =c
    c c2 > {
        c c2 -
        '+'
        put_repeated
    } {
        c2 c -
        '-'
        put_repeated
    } if call
    '.' putc
    '>' putc
    ln
    ;

: put_dialog
    0 =i
    { i max_pass < } {
        ',' putc
        '.' putc
        '>' putc
        i inc =i
    } while

    max_pass '<' put_repeated
    ln
    ;

: main
    16 =max_pass

    put_dialog
    'pass load

    0 =eof
    { eof not } {
        'buf load
        dump ln
        shift
    } while
    ;


