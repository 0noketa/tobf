t  n i j k v v2 v3 v4 e r
tmp t

# :main

# reserves single area for in/out
load resv 7

loadas a mem 256

unload resv
loadas in code mod_input

writeln_lit  number of elements:
in:readlnint n

# set 1 v2
inc v2
copy n v
ifnotin v 0 1
    dec v2
endifnotin v 0 1

ifelse v2 e
    writeln_lit  error.
    writeln_lit  number must be >=2
else v2 e
    writeln_lit  EOL separated elements:
    read_array a n in

    sort_array a n

    unload in
    loadas out code mod_print

    write_array a n out
endifelse v2 e

# end main


:for i j
    clear i
    while j
:endfor i j
        inc i
        dec j
    endwhile j

:read_array a n in
    copyadd n j
    for i j
        in:readlnint v
        a:@w_moveadd v i
    endfor i j

:sort_array a n
    # r is true when any swap happend
    inc r
    while r
        dec r
        set 1 k

        ! copyadd n | dec <- j
        for i j
            a:@r_copy i v v3
            a:@r_copy k v2 v4

            ifgt v3 v4 e
                a:@w_copy v2 i
                a:@w_copy v k

                set 1 r
            endifgt v3 v4 e

            inc k
        endfor i j
    endwhile r

:write_array a n out
    copyadd n j
    for i j
        a:@r_copy i v
        out:writelnint v
    endfor i j


end
